import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id 'jacoco'
    id "org.openapi.generator" version "7.5.0"
    id "com.vanniktech.maven.publish" version "0.31.0"
}

group = 'de.codebarista'
version = '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    compileOnly 'org.openapitools:jackson-databind-nullable:0.2.6' // for generated shopware model classes

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

/**
 * Configures the 'openApiGenerate' task to generate Java classes from the specified OpenAPI 3 specification file
 * for Shopware API integration.
 */
openApiGenerate {
    generatorName.set("java")
    inputSpec.set("$rootDir/shopware_openapi3.json")
    outputDir.set("$rootDir/generated/shopware")
    apiPackage.set("de.codebarista.shopware.api")
    modelPackage.set("de.codebarista.shopware.model")
    configOptions.put("dateLibrary", "java8")
    configOptions.put("library", "webclient") // use Spring webclient
    configOptions.put("useJakartaEe", "true") // Use Jakarta EE dependencies instead of javax
}

/**
 * Generates Shopware model classes and places them in the src/main/java/shopware source directory.
 * This task depends on the 'openApiGenerate' task.
 */
tasks.register('generateShopwareModelClasses', Copy) {
    def mainJavaSrcDir = sourceSets.main.java.srcDirs.iterator().next()
    def sourceDir = "$rootDir/generated/shopware/src/main/java/de/codebarista/shopware"
    from(sourceDir)
    into("${mainJavaSrcDir}/de/codebarista/shopware")
    dependsOn tasks.openApiGenerate
    doLast {
        delete "$rootDir/generated"
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

mavenPublishing {
    pom {
        name.set("Shopware Model")
        description.set("Auto-generated Java model classes for the Shopware Store API")
        url.set("https://github.com/codebarista-de/shopware-model")

        licenses {
            license {
                name.set("MIT License")
                url.set("https://opensource.org/licenses/MIT")
            }
        }

        developers {
            developer {
                id.set("codebarista.de")
                name.set("Codebarista")
            }
        }

        scm {
            connection.set("scm:git:git://github.com/codebarista-de/shopware-model.git")
            developerConnection.set("scm:git:ssh://github.com/codebarista-de/shopware-model.git")
            url.set("https://github.com/codebarista-de/shopware-model")
        }
    }

    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()
}

tasks.register('printVersion') {
    doLast {
        println project.version
    }
}
